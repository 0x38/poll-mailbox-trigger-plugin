
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'java'

idea.project?.vcs = 'Git'

ext {
    /* optional - set variable enforceJavaVersion */
    enforceJavaVersionFlag = project.getProperties().containsKey('enforceJavaVersion')
    javaVersion = project.getProperties().get('enforceJavaVersion') ?: JavaVersion.VERSION_1_6
    compileTasks = [compileJava, compileTestJava, compileGroovy, compileTestGroovy]
}

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"
}

ext {
    groovyVersion = '2.4.4'
    customJvmArgs = ['-Xmx128m']
}

configurations {
    providedCompile
}

sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile
sourceSets.test.runtimeClasspath += configurations.providedCompile

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'

    def jvmOpts = customJvmArgs.join(' ')
    inputs.property("jvmOpts", jvmOpts)
    doLast {
        def optsEnvVar = "DEFAULT_JVM_OPTS"
        scriptFile.write scriptFile.text.replace("$optsEnvVar=\"\"", "$optsEnvVar=\"$jvmOpts\"")
        batchScript.write batchScript.text.replace("set $optsEnvVar=", "set $optsEnvVar=$jvmOpts")
    }
}

allprojects {
	clean{
		delete "work"
		delete ".gradle"
	}
	
	tasks.withType(JavaCompile) {
        options.fork = true
        options.forkOptions.jvmArgs += customJvmArgs
	}
	
    tasks.withType(GroovyCompile) {
        configure(groovyOptions.forkOptions) {
            memoryMaximumSize = '256m'
            jvmArgs = customJvmArgs
        }
    }
    
    tasks.withType(Test) {
        jvmArgs(customJvmArgs)
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

//subprojects {
//    apply from: "$rootDir/gradle/idea.gradle"
//}

// enforce version...

task enforceJavaVersionTask << {
    if (enforceJavaVersionFlag){
        def foundVersion = JavaVersion.current()
        if (foundVersion.toString() != enforceJavaVersion.toString()){
            throw new IllegalStateException("Wrong Java version; required is $enforceJavaVersion, but found $foundVersion")
        }
    } else {
        println "Java version not enforced."
    }
}

compileTasks*.dependsOn(enforceJavaVersionTask);

task showMeCache << {
  configurations.compile.each { println it }
}

ext.verifySize = { File file, int expectedSizeKB ->
    def actualSizeBytes = file.length()
    println "Output file size is: ${(int) actualSizeBytes / 1024} KB"
    assert actualSizeBytes < (expectedSizeKB * 1024)
}
